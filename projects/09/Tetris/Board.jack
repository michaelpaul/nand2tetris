class Board
{
    // matrix do tabuleiro
    field Array b;
    // tamanho da matrix
    field int rows, cols, visible_rows;
    // posição do ponto origem da mesa no canto inferior esquerdo
    field int bx, by;
    // board width/height
    field int bw, bh;
    // peça atual
    field Piece current;

    constructor Board new(int max_rows, int max_cols)
    {
        var int n;

        let rows = max_rows;
        let cols = max_cols;
        let visible_rows = rows - 2;
        let bx = 190;
        let by = 250;
        let bw = cols * Block.getSize() + Block.getMargin();
        let bh = visible_rows * Block.getSize() + Block.getMargin();

        // enter the matrix
        let n = 0;
        let b = Array.new(rows);
        while (n < rows) {
            let b[n] = Array.new(cols);
            let n = n + 1;
        }

        return this;
    }

    // get cel
    method int getCel(int i, int j)
    {
        var Array row;
        var int val;
        //if (i < 0 || i > rows) {
        //    do Sys.error();
        //}
        let row = b[i];
        let val = row[j];
        return val;
    }

    // set cel
    method void setCel(int i, int j, int val)
    {
        var Array row;
        var int val;
        let row = b[i];
        let row[j] = val;
        return;
    }

    // adicionar uma peça no topo do tabuleiro
    method void addPiece(Piece piece)
    {
        var Block b;
        do piece.rewind();
        while (piece.valid()) {
            let b = piece.current();
            do setCel(b.getRow(), b.getCol(), b);
            do piece.next();
        }
        // store piece
        let current = piece;
        return;
    }

    method void removePiece(Piece piece)
    {
        var Block b;
        do piece.rewind();
        while (piece.valid()) {
            let b = piece.current();
            do setCel(b.getRow(), b.getCol(), 0);
            do piece.next();
        }
        return;
    }

    method boolean detectCollision(Piece piece)
    {
        var Block b;
        var int cel;
        var int row, col;

        do piece.rewind();
        while (piece.valid()) {
            let b = piece.current();

            let row = b.getRow();
            let col = b.getCol();

            // validar indices
            if (col < 0 | col > (cols - 1)) {
                return true;
            }
            if (row < 0 | row > (rows - 1)) {
                return true;
            }

            let cel = getCel(b.getRow(), b.getCol());

            if (~(cel = 0)) {
                return true;
            }
            do piece.next();
        }

        return false;
    }

    method void move(Piece piece, int dir)
    {
        var Piece copy;
        let copy = piece.copy();
        do copy.move(dir);
        do moveTo(piece, copy);
        do copy.dispose();
        return;
    }

    method void rotateLeft(Piece piece)
    {
        do rotate(piece, 3);
        return;
    }

    method void rotateRight(Piece piece)
    {
        do rotate(piece, 4);
        return;
    }

    method void rotate(Piece piece, int dir)
    {
        var Piece copy;
        let copy = piece.copy();
        do copy.rotate(dir);
        do moveTo(piece, copy);
        do copy.dispose();
        return;
    }

    method void moveTo(Piece piece, Piece copy)
    {
        do removePiece(piece);

        if (detectCollision(copy)) {
            do addPiece(piece);
            return;
        }

        do piece.updateBlocks(copy);
        do addPiece(piece);
        return;
    }

    /** {{{ Output */
    method void print()
    {
        var int i, j, val;

        do Output.printString("Board ");
        do Output.printInt(rows);
        do Output.printString("x");
        do Output.printInt(cols);
        do Output.println();

        let i = rows - 1;
        while (i > 0 | (i = 0)) {
            let j = 0;
            while (j < cols) {
                let val = b[i];
                let val = val[j];
                do Output.printInt(val);
                do Output.printString(" ");
                let j = j + 1;
            }
            do Output.println();
            let i = i - 1;
        }
        return;
    }

    method void drawBackground()
    {
        // black
        do Screen.setColor(true);
        do Screen.drawRectangle(bx, by - bh, bx + bw, by);
        // white
        do Screen.setColor(false);
        do Screen.drawRectangle(bx + 1, by - bh + 1, bx + bw - 1, by - 1);
        // restore color to black
        do Screen.setColor(true);
        return;
    }

    method void draw()
    {
        var int i, j, val;
        var Block block;

        do drawBackground();

        // Draw visible blocks
        let i = 0;
        while (i < visible_rows) {
            let j = 0;
            while (j < cols) {
                let val = getCel(i, j);
                if (val > 0) {
                    let block = val;
                    do block.draw(bx + Block.getMargin(), by);
                }
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }
    /** }}} */
}

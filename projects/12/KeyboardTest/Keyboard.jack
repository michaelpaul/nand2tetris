/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    /** Initializes the keyboard. */
    function void init() {
        return;
    }

    /**
     * Returns the ASCII code (as char) of the currently pressed key,
     * or 0 if no key is currently pressed.
     * Recognizes all ASCII characters, as well as the following extension
     * of action keys:
     * New line = 128 = String.newline()
     * Backspace = 129 = String.backspace()
     * Left Arrow = 130
     * Up Arrow = 131
     * Right Arrow = 132
     * Down Arrow = 133
     * Home = 134
     * End = 135
     * Page Up = 136
     * Page Down = 137
     * Insert = 138
     * Delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        var char key;
        let key = Memory.peek(24576);
        return key;
    }

    /**
     * Reads the next character from the keyboard.
     * waits until a key is pressed and then released, then echoes
     * the key to the screen, and returns the value of the pressed key.
     */
    function char readChar() {
        var char key, c;
        // filled cursor
        do Output.printChar(0);
        // wait press
        while (key = 0) {
            let key = Keyboard.keyPressed();
        }
        let c = key;
        // wait release
        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }
        // clear cursor
        do Output.backSpace();
        // non printable
        if ((c = 128) | (c = 129)) {
            return c;
        }
        do Output.printChar(c);
        return c;
    }

    /**
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its value.
     */
    function String readLine(String message) {
        var String s;
        var char c;
        
        do Output.printString(message);
        let s = String.new(64);
        
        while (true) {
            let c = Keyboard.readChar();
            // newline
            if (c = 128) {
                do Output.println();
                return s;
            }
            // backspace
            if (c = 129) {
                if (s.length() > 0) {
                    // remove um char, move cursor
                    do s.eraseLastChar();
                    do Output.backSpace();
                }
            } else {
                let s = s.appendChar(c);
            }
        }
        return s;
    }

    /**
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its
     * integer value (until the first non numeric character).
     */
    function int readInt(String message) {
        var String line;
        let line = Keyboard.readLine(message);
        return line.intValue();
    }
}
